{{- /*gotype: github.com/hashicorp/terraform-plugin-docs/internal/provider.ResourceTemplateType */ -}}
---
subcategory: "Transactional Email"
page_title: "Scaleway: scaleway_tem_webhook"
---

# Resource: scaleway_tem_webhook

Creates and manages Scaleway Transactional Email Webhooks.
For more information, refer to the [API documentation](https://www.scaleway.com/en/developers/api/transactional-email).

## Example Usage

### Basic

```terraform
resource "scaleway_tem_webhook" "main" {
  domain_id   = "your-domain-id"
  event_types = ["email_delivered", "email_bounced"]
  sns_arn     = "arn:scw:sns:fr-par:project-xxxx:your-sns-topic"
  name        = "example-webhook"
}
```

### Complete Example with Dependencies

```terraform
variable "domain_name" {
  type = string
}


resource "scaleway_mnq_sns" "sns" {
}

resource "scaleway_mnq_sns_credentials" "sns_credentials" {
  permissions {
    can_manage = true
  }
}

resource "scaleway_mnq_sns_topic" "sns_topic" {
  name       = "test-mnq-sns-topic-basic"
  access_key = scaleway_mnq_sns_credentials.sns_credentials.access_key
  secret_key = scaleway_mnq_sns_credentials.sns_credentials.secret_key
}

resource "scaleway_tem_domain" "cr01" {
  name       = var.domain_name
  accept_tos = true
}

resource "scaleway_domain_record" "spf" {
  dns_zone = var.domain_name
  type     = "TXT"
  data     = "v=spf1 ${scaleway_tem_domain.cr01.spf_config} -all"
}

resource "scaleway_domain_record" "dkim" {
  dns_zone = var.domain_name
  name     = "${scaleway_tem_domain.cr01.project_id}._domainkey"
  type     = "TXT"
  data     = scaleway_tem_domain.cr01.dkim_config
}

resource "scaleway_domain_record" "mx" {
  dns_zone = var.domain_name
  type     = "MX"
  data     = "."
}

resource "scaleway_domain_record" "dmarc" {
  dns_zone = var.domain_name
  name     = scaleway_tem_domain.cr01.dmarc_name
  type     = "TXT"
  data     = scaleway_tem_domain.cr01.dmarc_config
}

resource "scaleway_tem_domain_validation" "valid" {
  domain_id = scaleway_tem_domain.cr01.id
  region    = scaleway_tem_domain.cr01.region
  timeout   = 3600
}

resource "scaleway_tem_webhook" "webhook" {
  name        = "example-webhook"
  domain_id   = scaleway_tem_domain.cr01.id
  event_types = ["email_delivered", "email_bounced"]
  sns_arn     = scaleway_mnq_sns_topic.sns_topic.arn
  depends_on  = [scaleway_tem_domain_validation.valid, scaleway_mnq_sns_topic.sns_topic]
}
```

## Argument Reference

The following arguments are supported:

- `domain_id` - (Required) The ID of the domain the webhook is associated with.

- `event_types` - (Required) A list of event types that trigger the webhook.
- `sns_arn` - (Required) The Amazon Resource Name (ARN) of the SNS topic.
- `name` - (Optional) The name of the webhook. Defaults to an autogenerated name if not provided.
- `region` - (Defaults to provider region). The region in which the webhook should be created.
- `project_id` - (Defaults to provider project_id) The ID of the project the webhook is associated with.

## Attributes Reference

In addition to all arguments above, the following attributes are exported:

- `id` - The ID of the webhook.
- `organization_id` - The ID of the organization the webhook belongs to.
- `created_at` - The date and time of the webhook's creation (RFC 3339 format).
- `updated_at` - The date and time of the webhook's last update (RFC 3339 format).

## Import

Webhooks can be imported using the {region}/{id}, e.g.

```bash
terraform import scaleway_tem_webhook.main fr-par/11111111-1111-1111-1111-111111111111
```
