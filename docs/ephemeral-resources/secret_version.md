---
subcategory: "Secrets"
page_title: "Scaleway: scaleway_secret_version"
---

# scaleway_secret_version (Ephemeral Resource)

The [`scaleway_secret_version`](https://registry.terraform.io/providers/scaleway/scaleway/latest/docs/ephemeral-resources/secret_version) Ephemeral Resource is used to access and read secret versions from Scaleway Secret Manager.

Contrary to the secret_version data source, this is an [ephemeral resource](https://developer.hashicorp.com/terraform/plugin/framework/ephemeral-resources): it will not be persisted in plan or state artifacts.

This ephemeral resource is particularly useful when you need to access secret values during Terraform execution without persisting them in the state file, especially if that secret's data has been set with the [write-only](https://developer.hashicorp.com/terraform/language/manage-sensitive-data/write-only) attribute [`data_wo`](https://registry.terraform.io/providers/scaleway/scaleway/latest/docs/resources/secret_version#data_wo-2). You can avoid exposing sensitive data in your Terraform state files, from the creation to the access of your secret_version resource.

Refer to the Secret Manager [documentation](https://www.scaleway.com/en/docs/secret-manager/) and [API documentation](https://www.scaleway.com/en/developers/api/secret-manager/) for more information.


## Example Usage

```terraform
### Create a secret version and access it by its ID without ever persisting the data in the state file

resource "scaleway_secret" "main" {
  name        = "my-secret"
  description = "my-secret-description"
}

resource "scaleway_secret_version" "v1" {
  description     = "version1"
  secret_id       = scaleway_secret.main.id
  data_wo         = "my_super_secret_data"
  data_wo_version = 1
  depends_on      = [scaleway_secret.main]
}

# Access a specific secret version revision using the ephemeral resource with secret_id
ephemeral "scaleway_secret_version" "data_v1" {
  secret_id  = scaleway_secret.main.id
  revision   = "1"
  depends_on = [scaleway_secret_version.v1]
}

# Access the latest secret version using the ephemeral resource
ephemeral "scaleway_secret_version" "data_latest" {
  secret_id  = scaleway_secret.main.id
  revision   = "latest"
  depends_on = [scaleway_secret_version.v1]
}
```

```terraform
### Create a secret version and access it by its name without ever persisting the data in the state file

resource "scaleway_secret" "main" {
  name        = "my-secret"
  description = "my-secret-description"
}

resource "scaleway_secret_version" "v1" {
  description     = "version1"
  secret_id       = scaleway_secret.main.id
  data_wo         = "my_super_secret_data"
  data_wo_version = 1
  depends_on      = [scaleway_secret.main]
}

# Access a specific secret version revision using the ephemeral resource with secret_name
ephemeral "scaleway_secret_version" "data_v1" {
  secret_name = scaleway_secret.main.name
  revision    = "1"
  depends_on  = [scaleway_secret_version.v1]
}

# Access the latest secret version using the ephemeral resource
ephemeral "scaleway_secret_version" "data_latest" {
  secret_name = scaleway_secret.main.name
  revision    = "latest"
  depends_on  = [scaleway_secret_version.v1]
}
```



<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `organization_id` (String) The organization ID to filter the secret version
- `project_id` (String) The project ID to filter the secret version
- `region` (String) The region of the secret version. If not set, the region is derived from the secret_id when possible or from the provider configuration.
- `revision` (String) The revision of the secret version. Defaults to `latest`
- `secret_id` (String) The ID of the secret associated with the version. Either secret_id or secret_name must be specified.
- `secret_name` (String) The name of the secret.  Either secret_id or secret_name must be specified.

### Read-Only

- `created_at` (String) Date and time of secret version's creation (RFC 3339 format)
- `data` (String, Sensitive) The payload of the secret version (base64 encoded)
- `description` (String) Description of the secret version
- `status` (String) Status of the secret version
- `updated_at` (String) Date and time of secret version's last update (RFC 3339 format)


