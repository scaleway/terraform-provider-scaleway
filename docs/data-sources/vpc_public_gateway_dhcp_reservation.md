---
page_title: "Scaleway: scaleway_vpc_public_gateway_dhcp_reservation"
description: |- Get information about Scaleway VPC Public Gateway DHCP Reservation.
---

# scaleway_vpc_public_gateway_dhcp_reservation

Gets information about a dhcp entries. For further information please check the
API [documentation](https://developers.scaleway.com/en/products/vpc-gw/api/v1/#dhcp-entries-e40fb6)

## Example Dynamic

```hcl
resource scaleway_vpc_private_network main {
}

resource "scaleway_instance_server" "main" {
  image = "ubuntu_focal"
  type  = "DEV1-S"
  zone = "fr-par-1"

  private_network {
    pn_id = scaleway_vpc_private_network.main.id
  }
}

resource scaleway_vpc_public_gateway_ip main {
}

resource scaleway_vpc_public_gateway_dhcp main {
  subnet = "192.168.1.0/24"
}

resource scaleway_vpc_public_gateway main {
  name = "foobar"
  type = "VPC-GW-S"
  ip_id = scaleway_vpc_public_gateway_ip.main.id
}

resource scaleway_vpc_gateway_network main {
  gateway_id = scaleway_vpc_public_gateway.main.id
  private_network_id = scaleway_vpc_private_network.main.id
  dhcp_id = scaleway_vpc_public_gateway_dhcp.main.id
  cleanup_dhcp = true
  enable_masquerade = true
}

## Retrieve the dynamic entries generated by mac address
data "scaleway_vpc_public_gateway_dhcp_reservation" "by_mac_address" {
  mac_address = "${scaleway_instance_server.main.private_network.0.mac_address}"
}
```

## Example Static and PAT rule

```hcl
resource scaleway_vpc_private_network main {}

resource "scaleway_instance_security_group" main {
    inbound_default_policy  = "drop"
    outbound_default_policy = "accept"
    
    inbound_rule {
        action = "accept"
        port   = "22"
    }
}

resource "scaleway_instance_server" "main" {
    image = "ubuntu_focal"
    type  = "DEV1-S"
    zone = "fr-par-1"

    security_group_id = scaleway_instance_security_group.main.id

    private_network {
        pn_id = scaleway_vpc_private_network.main.id
    }
}

resource scaleway_vpc_public_gateway_ip main {
}

resource scaleway_vpc_public_gateway_dhcp main {
    subnet = "192.168.1.0/24"
}

resource scaleway_vpc_public_gateway main {
    name = "foobar"
    type = "VPC-GW-S"
    ip_id = scaleway_vpc_public_gateway_ip.main.id
}

resource scaleway_vpc_gateway_network main {
    gateway_id = scaleway_vpc_public_gateway.main.id
    private_network_id = scaleway_vpc_private_network.main.id
    dhcp_id = scaleway_vpc_public_gateway_dhcp.main.id
    cleanup_dhcp = true
    enable_masquerade = true
}

resource scaleway_vpc_public_gateway_dhcp_reservation main {
    gateway_network_id = scaleway_vpc_gateway_network.main.id
    mac_address = scaleway_instance_server.main.private_network.0.mac_address
    ip_address = "192.168.1.4"
}

### VPC PAT RULE
resource "scaleway_vpc_public_gateway_pat_rule" "main" {
    gateway_id   = scaleway_vpc_public_gateway.main.id
    private_ip   = scaleway_vpc_public_gateway_dhcp_reservation.main.ip_address
    private_port = 22
    public_port  = 2222
    protocol     = "tcp"
}

data "scaleway_vpc_public_gateway_dhcp_reservation" "by_id" {
    reservation_id = "${scaleway_vpc_public_gateway_dhcp_reservation.main.id}"
}
```

## Argument Reference

- `reservation_id`  (Required) The ID of the Reservation to retrieve
- `mac_address`  (Optional) The MAC address of the reservation to retrieve
- `zone` - (Defaults to [provider](../index.md#zone) `zone`) The [zone](../guides/regions_and_zones.md#zones) in which
  the image exists.

## Attributes Reference

The following arguments are exported:

- `gateway_network_id` - The ID of the owning GatewayNetwork.
- `ip_address` - The IP address to give to the machine (IP address).
- `hostname` - The Hostname of the client machine.
- `type` - The reservation type, either static (DHCP reservation) or dynamic (DHCP lease). Possible values are reservation and lease.
- `created_at` - The date and time of the creation of the public gateway DHCP config.
- `updated_at` - The date and time of the last update of the public gateway DHCP config.
